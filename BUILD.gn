# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/clang.gni")
import("//common/config.gni")
import("//examples/examples.gni")
import("//shell/platform/config.gni")
import("//shell/platform/glfw/config.gni")
import("//testing/testing.gni")
import("//third_party/dart/build/dart/copy_tree.gni")

# Whether to build the dartdevc sdk, libraries, and source files
# required for the flutter web sdk.
# TODO(jacksongardner): remove this poorly named argument once the recipes stop
# using it. https://github.com/flutter/flutter/issues/113303
declare_args() {
  full_dart_sdk = false
}

config("config") {
  include_dirs = [ ".." ]
  cflags = []
  if (is_win) {
    if (current_cpu != "x86") {
      cflags += [ "/WX" ]  # Treat warnings as errors.
    }
  }
  if (is_clang) {
    cflags += [ "-Wunreachable-code" ]
  }
}

config("export_dynamic_symbols") {
  # --dynamic-list is the GNU linker syntax supported by ELF linkers.
  # -exported_symbols_list is the macOS linker syntax. The different flags
  # accept files formatted differently, so we have exported_symbols.sym for GNU
  # linker syntax, and exported_symbols_mac.sym for the macOS linker syntax.
  if (is_linux || is_fuchsia) {
    inputs = [ "//common/exported_symbols.sym" ]
    ldflags = [ "-Wl,--dynamic-list=" + rebase_path(inputs[0], root_build_dir) ]
  } else if (is_mac) {
    inputs = [ "//common/exported_symbols_mac.sym" ]
    ldflags = [
      "-Xlinker",
      "-exported_symbols_list",
      "-Xlinker",
      rebase_path(inputs[0], root_build_dir),
    ]
  }
}

group("flutter") {
  testonly = true

  # Compile the engine.
  public_deps = [
    ":unittests",
    "//shell/platform/embedder:flutter_engine",
    "//sky",
  ]

  # Ensure the example for a sample embedder compiles.
  if (build_embedder_examples) {
    public_deps += [
      "//examples/glfw",
      "//examples/vulkan_glfw",
    ]

    if (!is_mac) {
      public_deps += [ "//examples/glfw_drm" ]
    }
  }

  # If enabled, compile the SDK / snapshot.
  if (!is_fuchsia) {
    public_deps += [ "//lib/snapshot:generate_snapshot_bins" ]

    if (build_engine_artifacts) {
      public_deps += [
        "//build/dart:dart_sdk",
        "//flutter_frontend_server:frontend_server",

        # This must be listed explicitly for desktop cross-builds since
        # //lib/snapshot:generate_snapshot_bin will only build
        # gen_snapshot for the host and not the target.
        "//third_party/dart/runtime/bin:gen_snapshot",

        # Impeller artifacts - compiler and libtessellator
        "//impeller/compiler:impellerc",
        "//impeller/tessellator:tessellator_shared",

        # path_ops
        "//tools/path_ops",
      ]

      if (host_os == "linux") {
        public_deps += [
          # Built alongside gen_snapshot for 64 bit targets
          "//third_party/dart/runtime/bin:analyze_snapshot",
        ]
      }

      if (full_dart_sdk) {
        public_deps += [ "//web_sdk" ]
      }
    }
  }

  if (build_engine_artifacts) {
    public_deps += [
      "//shell/testing",
      "//tools/const_finder",
      "//tools/font-subset",
    ]
  }

  # Compile all benchmark targets if enabled.
  if (enable_unittests && !is_win && !is_fuchsia) {
    public_deps += [
      "//display_list:display_list_benchmarks",
      "//display_list:display_list_builder_benchmarks",
      "//display_list:display_list_region_benchmarks",
      "//fml:fml_benchmarks",
      "//impeller/geometry:geometry_benchmarks",
      "//lib/ui:ui_benchmarks",
      "//shell/common:shell_benchmarks",
      "//third_party/txt:txt_benchmarks",
    ]
  }

  if ((flutter_runtime_mode == "debug" || flutter_runtime_mode == "profile") &&
      (is_ios || is_android)) {
    public_deps += [ "//testing/scenario_app" ]
  }

  if (is_android && flutter_runtime_mode == "profile" &&
      current_cpu == "arm64") {
    public_deps += [ "//testing/android_background_image" ]
  }
}

group("unittests") {
  testonly = true

  public_deps = []
  if (is_android) {
    public_deps += [ "//shell/platform/android:flutter_shell_native_unittests" ]
  }

  if (is_ios) {
    public_deps += [ "//shell/platform/darwin/ios:ios_test_flutter" ]
  }

  # Compile all unittests targets if enabled.
  if (enable_unittests) {
    public_deps += [
      "//display_list:display_list_rendertests",
      "//display_list:display_list_unittests",
      "//flow:flow_unittests",
      "//fml:fml_unittests",
      "//lib/ui:ui_unittests",
      "//runtime:dart_plugin_registrant_unittests",
      "//runtime:no_dart_plugin_registrant_unittests",
      "//runtime:runtime_unittests",
      "//shell/common:shell_unittests",
      "//shell/platform/embedder:embedder_a11y_unittests",
      "//shell/platform/embedder:embedder_proctable_unittests",
      "//shell/platform/embedder:embedder_unittests",
      "//testing:testing_unittests",
      "//testing/dart",
      "//testing/smoke_test_failure",
      "//third_party/tonic/tests:tonic_unittests",
      "//third_party/txt:txt_unittests",
    ]

    # The accessibility library only supports Mac and Windows at the moment.
    if (is_mac || is_win) {
      public_deps += [ "//third_party/accessibility:accessibility_unittests" ]
    }

    if (is_fuchsia) {
      public_deps += [ "//shell/platform/fuchsia:tests" ]
    }

    if (is_mac || is_linux || is_win) {
      public_deps += [
        "//impeller:impeller_dart_unittests",
        "//impeller:impeller_unittests",
      ]
    }

    if (is_mac) {
      public_deps += [
        "//impeller/golden_tests:impeller_golden_tests",
        "//shell/platform/darwin/common:framework_common_unittests",
        "//third_party/spring_animation:spring_animation_unittests",
      ]
    }

    if (!is_win && !is_fuchsia) {
      public_deps += [
        "//shell/platform/android/external_view_embedder:android_external_view_embedder_unittests",
        "//shell/platform/android/jni:jni_unittests",
        "//shell/platform/android/platform_view_android_delegate:platform_view_android_delegate_unittests",
      ]
    }

    # Unit tests for desktop embeddings should only be built if the desktop
    # embeddings are being built.
    if (enable_desktop_embeddings) {
      public_deps += [
        "//shell/platform/common:common_cpp_core_unittests",
        "//shell/platform/common/client_wrapper:client_wrapper_unittests",
      ]

      if (!is_fuchsia) {
        # These tests require the embedder and thus cannot run on fuchsia.
        # TODO(): Enable when embedder works on fuchsia.
        public_deps += [ "//shell/platform/common:common_cpp_unittests" ]

        # These tests require GLFW and thus cannot run on fuchsia.
        public_deps += [
          "//shell/platform/glfw/client_wrapper:client_wrapper_glfw_unittests",
        ]
      }

      if (is_linux) {
        public_deps += [ "//shell/platform/linux:flutter_linux_unittests" ]
        if (build_glfw_shell) {
          public_deps += [ "//shell/platform/glfw:flutter_glfw_unittests" ]
        }
      }

      if (is_mac) {
        public_deps +=
            [ "//shell/platform/darwin/macos:flutter_desktop_darwin_unittests" ]
      }

      if (is_win) {
        public_deps += [
          "//shell/platform/windows:flutter_windows_unittests",
          "//shell/platform/windows/client_wrapper:client_wrapper_windows_unittests",
        ]
      }
    }
  }
}

if (build_engine_artifacts) {
  group("archives") {
    testonly = true

    deps = [ "//build/archives:artifacts" ]
  }
}

group("dist") {
  testonly = true

  deps = [ "//sky/dist" ]
}

if (is_fuchsia && enable_unittests) {
  group("fuchsia_tests") {
    testonly = true

    deps = [ "//shell/platform/fuchsia:tests" ]
  }
}

# On Windows, when targeting Android, we only build gen_snapshot. This
# top-level target provides a convenient shorthand for the full path into the
# Dart tree, and is less ambiguous than specifying the binary to build since
# we can specify the toolchain to use, too.
if (host_os == "win") {
  _gen_snapshot_target =
      "//third_party/dart/runtime/bin:gen_snapshot($host_toolchain)"
  copy("gen_snapshot") {
    deps = [ _gen_snapshot_target ]

    gen_snapshot_out_dir = get_label_info(_gen_snapshot_target, "root_out_dir")
    sources = [ "$gen_snapshot_out_dir/gen_snapshot.exe" ]
    outputs = [ "$root_build_dir/gen_snapshot/gen_snapshot.exe" ]
  }
}
