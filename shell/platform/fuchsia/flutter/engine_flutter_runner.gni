# Copyright 2013 The Flutter Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

assert(is_fuchsia)

import("//build/fuchsia/sdk.gni")

# Builds a flutter_runner
#
# Parameters:
#
#  output_name (required):
#    The name of the resulting binary.
#
#  extra_deps (required):
#    Any additional dependencies.
#
#  product (required):
#    Whether to link against a Product mode Dart VM.
#
#  extra_defines (optional):
#    Any additional preprocessor defines.
template("flutter_runner") {
  assert(defined(invoker.output_name), "flutter_runner must define output_name")
  assert(defined(invoker.extra_deps), "flutter_runner must define extra_deps")
  assert(defined(invoker.product), "flutter_runner must define product")

  invoker_output_name = invoker.output_name
  extra_deps = invoker.extra_deps

  extra_defines = []
  if (defined(invoker.extra_defines)) {
    extra_defines += invoker.extra_defines
  }

  executable(target_name) {
    output_name = invoker_output_name

    defines = extra_defines

    libs = []

    sources = [
      "accessibility_bridge.cc",
      "accessibility_bridge.h",
      "component.cc",
      "component.h",
      "engine.cc",
      "engine.h",
      "fuchsia_intl.cc",
      "fuchsia_intl.h",
      "isolate_configurator.cc",
      "isolate_configurator.h",
      "logging.h",
      "main.cc",
      "runner.cc",
      "runner.h",
      "scenic_compositor_connection.cc",
      "scenic_compositor_connection.h",
      "scenic_platform_handler.cc",
      "scenic_platform_handler.h",
      "scenic_scene_controller.cc",
      "scenic_scene_controller.h",
      "unique_fdio_ns.h",
      "vsync_recorder.cc",
      "vsync_recorder.h",
      "vsync_waiter.cc",
      "vsync_waiter.h",
    ]

    # The use of these dependencies is temporary and will be moved behind the
    # embedder API.
    flutter_legacy_deps = [
      "$flutter_root/common",
      "$flutter_root/fml",
      "$flutter_root/lib/ui",
      "$flutter_root/runtime",
      "$flutter_root/shell/common",
    ]

    # TODO(kaushikiska) evaluate if all of these are needed.
    fuchsia_deps = [
      "$flutter_root/shell/platform/fuchsia/dart-pkg/fuchsia",
      "$flutter_root/shell/platform/fuchsia/dart-pkg/zircon",
      "$flutter_root/shell/platform/fuchsia/runtime/dart/utils",
    ]

    deps = [
      "$flutter_root/shell/platform/embedder",
      "$flutter_root/shell/platform/embedder:embedder_gpu_configuration",
      "$fuchsia_sdk_root/fidl:fuchsia.accessibility.semantics",
      "$fuchsia_sdk_root/fidl:fuchsia.fonts",
      "$fuchsia_sdk_root/fidl:fuchsia.images",
      "$fuchsia_sdk_root/fidl:fuchsia.intl",
      "$fuchsia_sdk_root/fidl:fuchsia.io",
      "$fuchsia_sdk_root/fidl:fuchsia.sys",
      "$fuchsia_sdk_root/fidl:fuchsia.ui.app",
      "$fuchsia_sdk_root/fidl:fuchsia.ui.gfx",
      "$fuchsia_sdk_root/fidl:fuchsia.ui.input",
      "$fuchsia_sdk_root/fidl:fuchsia.ui.scenic",
      "$fuchsia_sdk_root/fidl:fuchsia.ui.views",
      "$fuchsia_sdk_root/pkg:async-cpp",
      "$fuchsia_sdk_root/pkg:async-default",
      "$fuchsia_sdk_root/pkg:async-loop",
      "$fuchsia_sdk_root/pkg:async-loop-cpp",
      "$fuchsia_sdk_root/pkg:fdio",
      "$fuchsia_sdk_root/pkg:fidl_cpp",
      "$fuchsia_sdk_root/pkg:scenic_cpp",
      "$fuchsia_sdk_root/pkg:sys_cpp",
      "$fuchsia_sdk_root/pkg:syslog",
      "$fuchsia_sdk_root/pkg:trace",
      "$fuchsia_sdk_root/pkg:trace-engine",
      "$fuchsia_sdk_root/pkg:trace-provider-so",
      "$fuchsia_sdk_root/pkg:vfs_cpp",
      "$fuchsia_sdk_root/pkg:zx",
      "//third_party/skia",
      "//third_party/tonic",
    ] + fuchsia_deps + flutter_legacy_deps + extra_deps

    # The flags below are needed so that Dart's CPU profiler can walk the
    # C++ stack.
    cflags = [ "-fno-omit-frame-pointer" ]

    if (!invoker.product) {
      # This flag is needed so that the call to dladdr() in Dart's native symbol
      # resolver can report good symbol information for the CPU profiler.
      ldflags = [ "-rdynamic" ]
    }
  }
}
